// Lark grammar for parsing asma paths

start: sep? dataset_id sep source_id sep _root_dir

_root_dir: batch_id sep (_images_root_dir | _illum_root_dir | _images_aligned_root_dir | _workspace_root_dir)

_images_root_dir: "images"i sep plate_id sep _plate_root_dir
_illum_root_dir: "illum"i sep plate_id sep leaf_node
_images_aligned_root_dir: "images_aligned"i sep "barcoding" sep plate_well_site_id sep leaf_node
_workspace_root_dir: "workspace" sep workspace_root_dir
_plate_root_dir: _sbs_folder | _cp_folder

_sbs_folder: magnification "_" "c" cycle_id "_SBS-" cycle_id _timestamp? sep? (_sbs_images | _sbs_metadata)?
_sbs_images: "Well" well_id "_Point" well_id "_" site_id "_Channel" (channel " nm,")* (channel ",")* (channel " nm" | channel)  "_Seq" seq_value "." extension
_sbs_metadata: leaf_node

_cp_folder: magnification "_CP_" _folder_plate_id _timestamp? sep? (_cp_images |_cp_metadata)?
_cp_images: "Well" well_id "_Point" well_id "_" site_id "_Channel" (channel " nm,")* (channel ",")* (channel " nm" | channel) "_Seq" seq_value "." extension

seq_value: DIGIT~1..4
_cp_metadata: leaf_node

workspace_root_dir: string
dataset_id: stringwithdashcommaspace
source_id: string
sep: "/"
batch_id: stringwithdashcommaspace
plate_id: string
_folder_plate_id: string
_timestamp: ("_" (DIGIT | "_")+)
magnification: DIGIT~2 "X"
cycle_id: DIGIT~1..2
well_id: (LETTER | DIGIT)~2
site_id: DIGIT~1..4
plate_well_site_id: plate_id "-Well" well_id "-" site_id
channel: stringwithdash
leaf_node: filename "." extension
filename: stringwithdashcommaspace
extension: stringwithdots
string: (LETTER | DIGIT)+
stringwithdash: (string | "-")+
stringwithdashcommaspace: ( string | "-" | "_" | "," | " " )+
stringwithdots: ( string | "." )+
DIGIT: "0".."9"

%import common.LETTER
